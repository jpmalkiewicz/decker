// This mission turns itself into one of the missions from Decker 1.12 when it
// becomes available. All the missions in the five standard_mission_*.files are
// identical. This means there can be five Decker 1.12 missions available at
// any given time


english
	available = true
	reload_when_over = true


	// the actual mission initialization happens when this trigger is executed
	trigger status == AVAILABLE
		deadline = random(0,4)

		flags = ""      // this variable will be used to collect the different flags for this mission
		goal_count = 1


		// make the target system rating similar to the player's base_mission_rating
		b = player.mission_base_rating + ( random(0,9) == 0 ? 0 : random(-1,1) + random(-1,1) )
		if b < 1
			b = 1
		if b > 20
			b = 20
		// pick a system with the chosen rating that is not currently used by another standard mission
		a = (b-1) * 5 + random(0,4)
		while SYSTEM_IN_USE(a)
			a = a + 1
			if a / 5 * 5 == a
				a = a - 5
		target_system = player.standard_mission_data.system[a]
		MARK_SYSTEM_IN_USE(target_system)


		// check whether the default mission goal should be used
		if target_system.type.default_mission_target != UNDEFINED && random(1,100) <= target_system.type.default_mission_target.probability
			b = target_system.type.default_mission_target
		else
			b = UNDEFINED
		a = random(1,100)

		// generate the mission goal
		if b.category == "file" ||( b == UNDEFINED && a <= 45 )              // the mission target is a file
			if b != UNDEFINED
				b = b.name
			else
				b = target_system.type.file[random(0,target_system.type.file.size-1)]
				
			if random(1,40) <= target_system.rating
				goal_count = random(2,5)
				flags = flags + goal_count
				
			if a < 15
				title = "Steal Files"
				description = "A client needs access to "+b+" from the "+target_system.name+" system. You must recover a copy of this data."
				a = 0
				while a < goal_count
					a = a + 1
					goal[] = DOWNLOAD_FILE
						target = FILE
							description = b
			else if a < 25
				title = "Steal & Erase Files"
				description = "A client desires sole access to "+b+" from the "+target_system.name+" system. You must recover a copy of this data and erase it from the system."
				a = 0
				while a < goal_count
					a = a + 1
					goal[] = DOWNLOAD_AND_ERASE_FILE
						target = FILE
							description = b
			else if a < 35
				title = "Erase Files"
				description = "A client wishes to destroy "+b+" located on the "+target_system.name+" system. You must locate this data and erase it from the system."
				a = 0
				while a < goal_count
					a = a + 1
					goal[] = ERASE_FILE
						target = FILE
							description = b
			else
				title = "Edit Files"
				description = "A client wishes to alter "+b+" located on the "+target_system.name+" system. You must locate this data in the system and edit it to the client's specifications."
				a = 0
				while a < goal_count
					a = a + 1
					goal[] = EDIT_FILE
						target = FILE
							description = b
							
			if goal_count > 1
				description = description + " There will be " + goal_count + " targets."

			if ( goal[0].structure_type != "EDIT_FILE" && random(1,5) == 1 ) || ( goal[0].structure_type == "EDIT_FILE" && random(1,5) <= 2 )
				goal[] = goal[0]
				goal[0] = NO_RED_ALARM
				flags = flags + "N"
				description = description + " The target system must not become aware of any tampering."


		else if ( b != UNDEFINED && b.category != "file" ) || ( b == UNDEFINED && a < 85 )          // the target is an IO node
			// determine the mission target
			if b != UNDEFINED
				c = b.category
				b = b.name
			else
				// pick a random IO node type
				a = random (0,target_system.type.deactivate_io_target.size + target_system.type.activate_io_target.size + target_system.type.sabotage_io_target.size - 1)
				if a < target_system.type.deactivate_io_target.size
					b = target_system.type.deactivate_io_target[a]
					c = "deactivate_io_target"
				else
					a = a - target_system.type.deactivate_io_target.size
					if a < target_system.type.activate_io_target.size
						b = target_system.type.activate_io_target[a]
						c = "activate_io_target"
					else
						b = target_system.type.sabotage_io_target[a-target_system.type.activate_io_target.size]

						c = "sabotage_io_target"

			if random(1,40) <= target_system.rating
				goal_count = random(2,5)
				flags = flags + goal_count
			
			if c == "deactivate_io_target"
				title = "Deactivate I/O"
				if goal_count == 1
					description = "A client wishes to deactivate "+b+" from the "+target_system.name+" system. You must locate the I/O node controlling this and deactivate it."
				else
					description = "A client wishes to deactivate "+b+" from the "+target_system.name+" system. You must locate the "+goal_count+" I/O nodes controlling this and deactivate them."
			else if c == "activate_io_target"
				title = "Activate I/O"
				if goal_count == 1
					description = "A client wishes to activate "+b+" from the "+target_system.name+" system. You must locate the I/O node controlling this and activate it."
				else
					description = "A client wishes to activate "+b+" from the "+target_system.name+" system. You must locate the "+goal_count+" I/O nodes controlling this and activate them."
			else
				title = "Sabotage"
				if goal_count == 1
					description = "A client wishes to sabotage "+b+" at the "+target_system.name+" system. You must locate the I/O node controlling this and sabotage it."
				else
					description = "A client wishes to sabotage "+b+" at the "+target_system.name+" system. You must locate the "+goal_count+" I/O nodes controlling this and sabotage them."
			
			a = 0
			while a < goal_count
				a = a + 1
				goal[] = MANIPULATE_IO
// target should be a node, not a node description !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
					target = b
					incomplete = (c == "deactivate_io_target") ? "IO node has not been deactivated" : (c == "activate_io_target") ? "IO node has not been activated" : "IO node has not been sabotaged"
					completed  = (c == "deactivate_io_target") ? "IO node has been deactivated"     : (c == "activate_io_target") ? "IO node has been activated"     : "IO node has been sabotaged"

			if random(1,5) == 1
				goal[] = goal[0]
				goal[0] = NO_RED_ALARM
				flags = flags + "N"
				description = description + " The target system must not become aware of any tampering."


		else if a < 90
			title = "Crash System"
			description = "A client wishes to crash the "+target_system.name+" system. You must locate the CPU of the system and initiate a system failure."
			goal[] = CRASH_SYSTEM
			
			
		else if a < 95
			title = "Create Backdoor"
			description = "A client wishes for a new backdoor to be created into the "+target_system.name+" system. You must locate the CPU for the system and create this back door. The client's decker will take care of the rest."
			goal[] = CREATE_BACKDOOR
			if random(1,2) == 1
				goal[] = goal[0]
				goal[0] = NO_RED_ALARM
				flags = flags + "N"
				description = description + " The target system must not become aware of any tampering."


		else
			title = "Run Program"
			
			// create the program the player will have to run
			// base the name of the hack program on the index of the target system so we don't get duplicate names
			c = player.standard_mission_data.system.size-1
			while player.standard_mission_data.system[c] != target_system
				c = c - 1
			c = random("a","z") + random("a","z") + random("a","z") + c + random(0,9) + random(0,9) + random(0,9) + random(0,9)
			b = THING
				name = "Client Program " + c
				item_rating = target_system.rating
				size = item_rating * 4
				status = INSTALLED
				type = THING_TYPE
					value = 0
					type = ARRAY
						SPECIAL

			a = random(1,3)   // 1 = IO node, 2 = DS node, 3 = CPU
			if a != 3 && random(1,40) <= target_system.rating
				goal_count = random(2,5)
				flags = flags + goal_count
				description = "A client needs a special program activated on the "+target_system.name+" system. Locate the target nodes by running the program, then activate the program within each nodes. You must remain in each target node until the program has finished running. Upon accepting the mission you will receive the client's program "+c+"."
			else
				description = "A client needs a special program activated on the "+target_system.name+" system. Locate the target node by running the program, then activate the program within the node. You must remain in the node until the program has finished running. Upon accepting the mission you will receive the client's program "+c+"."
				
			c = 0
			while c < goal_count
				c = c + 1
				goal[] = RUN_PROGRAM
// run program target must be a node, not a node description !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
					target = a
					program = b
					execution_time = 6 + b.item_rating / 2
					incomplete = "Program has not been run in node"
					completed = "Program was successfully run in node"
				
			if random(1,5) == 1
				goal[] = goal[0]
				goal[0] = NO_RED_ALARM
				flags = flags + "N"
				description = description + " The target system must not become aware of any tampering."
				


		// if the target system rating is 5 or higher, there's a 10% chance that the mission will be timed
		if target_system.rating >= 5 && random(1,10) == 1
			flags = flags + "T"
			deadline = 0
			a = goal[0].structure_type == "NO_RED_ALARM" ? 1 : 0
			goal[] = goal[a]
			goal[a] = TIME_RESTRICTION
				end_time = 180 * ((target_system.rating+3)/4)       // 3 minutes per area within the target system
			description = description + " The mission must be completed within "+(goal[a].end_time/60)+" minutes of system entry."
		

		// append the mission flags to the title
		if flags != ""
			title = title + " (" + flags + ")"


		payment = (120 - 10*deadline) * ( DEFAULT_MISSION_RATING() + player.lifestyle ) * (85 + random(0,30)) / 100
	
	
	trigger status == DESTROYED
		UNMARK_SYSTEM_IN_USE(target_system)
