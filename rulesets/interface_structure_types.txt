english
// interface component types **************************************************************************************************************************

	structure

		BORDER
			x
			y
			width
			height
			inverted = false
			top_color = @BORDER_COLOR1
			left_color
			right_color
			bottom_color = @BORDER_COLOR2
			background_color
			component
			thickness = 2


		BORDER_BUTTON
			expandable = true
			x
			y
			component
			state = IDLE
			idle              // the image for the idle state of the button. if the image name ends with a 0 and no pressed, hover or disabled images are defined, the program will look for images of the same name but ending in a 1, 2 and 3 for them
			// pressed
			// disabled
			// hover
			// on_mouse_up    // put the function with the button effect here, for a standard button
			// text_padding    // put a value here if you want the text to be shifted a different number of pixels than 4 from the top


		BUTTON
			expandable = true
			x
			y
			state = IDLE
			idle              // the image for the idle state of the button. if the image name ends with a 0 and no pressed or disabled images are defined, the program will look for images of the same name but ending in a 1 and 2 for them
			component
			// pressed
			// disabled
			// hover
			// on_mouse_up    // put the function with the button effect here, for a standard button


		COMPONENT             // all variables in each view definition are optional
			expandable = true
			x
			y
			// width
			// height
			// title             // only relevant if it is the top level component. then it sets the frame title
			// background_image
			// component         // must contain an array which contains the interface elements of this view, e.g. buttons and lists


		DRAWING_BOUNDARY // limits the area where things can be drawn to the given rectangle. all drawings which are in the component array of the DRAWING_BOUNDARY get cut off at the edge of the rectangle and only display inside
			x
			y
			width
			height
			component


		EVENT_BLOCKER    // the event handler will stop at this component with all input events. on_draw events pass through
			on_draw
			on_key_down      = &trueFunction
			on_mouse_down    = &trueFunction
			on_mouse_dragged = &trueFunction
			on_mouse_moved   = &trueFunction
			on_mouse_up      = &trueFunction
			x = -900000
			y = x
			width = -2*x
			height = -2*x


		IMAGE
			expandable = true
			image
			
			
		LINE
			x
			y
			x2
			y2
			color


		RECTANGLE
			x
			y
			width
			height
			color


		SCREEN_BACKGROUND
			x
			y
			width = "100%"
			height = "100%"
			component
			initializer = FUNCTION
				component = ARRAY
					createBorder(0,0)
					UNDEFINED
			on_resize = FUNCTION (new_width, new_height)
				component[0].width = new_width
				component[0].height = new_height
				if @BACKGROUND_COLOR2 == UNDEFINED
					component[0].background_color = @BACKGROUND_COLOR
					if component[1] != UNDEFINED
						component[1] = UNDEFINED
				else
					if component[1] == UNDEFINED
						component[1] = createBorder(5, 5, 0, 0)
					component[1].width = new_width-10
					component[1].height = new_height-10
					component[1].background_color = @BACKGROUND_COLOR
					component[0].background_color = @BACKGROUND_COLOR2


		SCROLLBAR  // a vertical scrollbar
			expandable = true
			x
			y
			width
			height
			component
			slider_position = 0  // logical position
			slider_max           // you HAVE to set this to a positive number or the slider won't work
			slider_stepping = 20 // this is the amount by which slider_position will change each time you press one of the arrow buttons
			effect               // put a function here. it gets called whenever the slider_position changes
			vertical = true      // true -> the scrollbar is vertical. all other values -> it's horizontal
			slider
			minus_button
			plus_button          // slider, minus_button, plus_button contain the structures or image names from which the scrollbar slider and the optional buttons on the scrollbar's ends are created


		SCROLLPANE
			x
			y
			width
			height
			component                            // component[2] optional vertical scrollbar, component[3] optional horizontal scrollbar, component[1] optional view element that gets displayed in the corner that is cut off if both scrollbars are displayed, component[0] a DRAWING_BOUNDARY with component[0].component being a wrapper around the view element that is displayed in this scrollpane
			displayed_element                    // the component that this SCROLLPANE displays
			optional_scrollbars = true           // the scrollbars are optional by default and only get displayed when needed
			vertical_scrollbar                   // either a scrollbar or an array containing the slider and buttons, or their names
			horizontal_scrollbar                 // like vertical_scrollbar
			corner_filler                        // component to be displayed in the cornerthat gets get cut off when both scrollbars are displayed
			scrollbar_padding = 1                // number of pixels to be put left empty between the DRAWING_PANE and the scrollbar
			initializer = FUNCTION
				component = ARRAY
					DRAWING_BOUNDARY
						x = 0
						y = 0
						component = COMPONENT   // wraps around the displayed component to be able to move it around on the screen without changing it
							x = 0
							y = 0
							component = UNDEFINED  // the displayed component
					UNDEFINED
					UNDEFINED
					UNDEFINED
			on_draw = FUNCTION
				LOCAL.w = width                  // width of the space available for displaying the displayed_element after adding scrollbars
				LOCAL.h = height                 // height of the space available for displaying the displayed_element after adding scrollbars
				LOCAL.dw = pixelwidth(displayed_element)   // width of the displayed_element
				LOCAL.dh = pixelheight(displayed_element)  // height of the displayed_element
				LOCAL.vscrollbar = false
				LOCAL.hscrollbar = false
				// determine whether we need a vertical scrollbar
				if ( !optional_scrollbars || h < dh ) && ( value_type(vertical_scrollbar) == "STRUCTURE" || value_type(vertical_scrollbar) == "ARRAY" )
					vscrollbar = value_type(vertical_scrollbar) == "ARRAY" ? createSimpleScrollbar(w, h, vertical_scrollbar, true) : vertical_scrollbar
					w = w - pixelwidth(vscrollbar) - scrollbar_padding
				// determine whether we need a horizontal scrollbar
				if ( !optional_scrollbars || w < dw ) && ( value_type(horizontal_scrollbar) == "STRUCTURE" || value_type(horizontal_scrollbar) == "ARRAY" )
					hscrollbar = value_type(horizontal_scrollbar) == "ARRAY" ? createSimpleScrollbar(w, h, horizontal_scrollbar, false) : horizontal_scrollbar
					h = h - pixelheight(hscrollbar) - scrollbar_padding
					// check whether after adding the horizontal scrollbar we now need the vertical scrollbar (w, h, because the display area is no longer high enough to display the content). we don't have to check whether scrollbars are optional, they definitely are
					if h < dh && ( value_type(vertical_scrollbar) == "STRUCTURE" || value_type(vertical_scrollbar) == "ARRAY" )
						vscrollbar = value_type(vertical_scrollbar) == "ARRAY" ? createSimpleScrollbar(w, h, vertical_scrollbar, true) : vertical_scrollbar
						w = w - pixelwidth(vscrollbar) - scrollbar_padding
					else
						vscrollbar.height = h   // the height needs to be reduced to make room for the horizontal scrollbar
				// add/remove the vertical scrollbar
				if vscrollbar != false
					vscrollbar.slider_max = dh - h
					// if the vertical scrollbar already exists, fetch the current state
					if component[2].structure_type == "SCROLLBAR"
						setSliderPosition(vscrollbar, component[2].slider_position)
						vscrollbar.slider_stepping = component[2].slider_stepping
						vscrollbar.slider_dragging = component[2].slider_dragging
						if vscrollbar.slider_dragging != UNDEFINED
							vscrollbar.slider.state = PRESSED
						else
							vscrollbar.slider.state = component[2].slider.state
						if vscrollbar.minus_button != UNDEFINED && component[2].minus_button != UNDEFINED
							vscrollbar.minus_button.state = component[2].minus_button.state
						if vscrollbar.plus_button != UNDEFINED && component[2].plus_button != UNDEFINED
							vscrollbar.plus_button.state = component[2].plus_button.state
					component[2] = vscrollbar
				else
					component[2] = UNDEFINED
				// add/remove the horizontal scrollbar
				if hscrollbar != false
					hscrollbar.slider_max = dw - w
					// if the horizontal scrollbar already exists, fetch the current state
					if component[3].structure_type == "SCROLLBAR"
						setSliderPosition(hscrollbar, component[3].slider_position)
						hscrollbar.slider_stepping = component[3].slider_stepping
						hscrollbar.slider_dragging = component[3].slider_dragging
						if hscrollbar.slider_dragging != UNDEFINED
							hscrollbar.slider.state = PRESSED
						else
							hscrollbar.slider.state = component[3].slider.state
						if hscrollbar.minus_button != UNDEFINED && component[3].minus_button != UNDEFINED
							hscrollbar.minus_button.state = component[3].minus_button.state
						if hscrollbar.component[3] != UNDEFINED && component[3].plus_button != UNDEFINED
							hscrollbar.component[3].state = component[3].plus_button.state
					component[3] = hscrollbar
				else
					component[3] = UNDEFINED
				// add or remove the corner filler
				if hscrollbar != false && vscrollbar != false
					component[1] = corner_filler
				else
					component[1] = UNDEFINED
				// adjust the displayed content
				component[0].width = w
				component[0].height = h
				component[0].component.x = hscrollbar == false ? 0 : -hscrollbar.slider_position
				component[0].component.y = vscrollbar == false ? 0 : -vscrollbar.slider_position
				component[0].component.component = displayed_element


		TABLE
			expandable = true
			width = 0
			height = 0
			rows = 0                 // number of rows in this table
			columns                  // number of columns in this table
			cell                     // two dimensional array containing the cells
			selected_row             // 0 - (rows-1) = row selected, all other values = no row selected
			selected_row_background  // if this is non-UNDEFINED, rows can be selected
			can_drag_rows            // set this to true to enable rearranging the table by dragging rows. will be set to the y value of the current drag point while a row is getting dragged
			cell_height              // all rows have the same height
			cell_width               // array containing the width for each column
			//cell_h_align           // default horizontal alignment of cell content. if it's defined in an ARRAY it's entries are used as the default horizontal alignment of cell content for each column. this value will only be used if the cell content doesn't have a valid value for v_align. watch out, the alignment setting gets set for the cell content. if the same thing is displayed in different cells (even from different tables), the cell that gets displayed first determines its alignment, if cell_alignment_x is set
			//cell_v_align           // default vertical alignment of cell content, works like cell_h_align
			//cell_x // x value of cell content. works like cell_h_align
			//cell_y // y value of cell content. works like cell_h_align
			// on_row_drag           // put an event handler function here if you want to react to rows getting dragged
			// on_selection_change   // put an event handler function here if you want to react to rows getting selected or deselected by the player
			initializer = FUNCTION
				cell = ARRAY


		TEXT
			x
			y
			v_align
			h_align
			width
			height
			text
			font           // a string, may contain several ; separated font settings (in arbitrary order, but later settings override earlier ones) : font face (Arial), font size (10pt) and font style (plain, bold, italic), e.g. "Arial;10pt;plain" or "bold;italic;Courier New;12pt". if style is omitted, plain is used. if font face or font size is omitted, the current settings are used, not the settings from the parent view element
			color          // a color, using the html color format #rrggbb


		TEXTFIELD
			x
			y
			v_align
			h_align
			width            // if you set the width of the TEXTFIELD, the text input will be constrained so the text + cursor will always fit into the field
			height
			text
			font
			color
			cursor = "_"
			char_limit       // the maximum number of characters for the text input
